buildscript {
    repositories {
        mavenCentral()
        mavenLocal()
    }
    // gradle plugin for scalatest (이 것이 없으면 FunSuite 로 된 것은 테스트를 못한다)
    dependencies {
        classpath 'com.github.maiflai:gradle-scalatest:0.4'
    }
}

apply plugin: 'scala'
apply plugin: 'maven'
apply plugin: 'scalatest'

group = 'kr.debop'
version = '0.1.0-SNAPSHOT'

configure(install.repositories.mavenInstaller) {
    pom.project {
        groupId 'kr.debop'
        artifactId 'tomcat-session-redis'
        version '0.1.0-SNAPSHOT'
    }
}

ext {
    jdkVersion = '1.7'
    scalaVersion = '2.11.1'
    scalaMainVersion = '2.11'
    akkaVersion = '2.3.4'

    slf4jVersion = '1.7.7'
    scalatraVersion = '2.3.0'
}

compileScala {
    options.encoding = 'UTF-8'
    sourceCompatibility = jdkVersion
    targetCompatibility = jdkVersion
}

dependencies {

    compile "com.typesafe.akka:akka-actor_$scalaMainVersion:$akkaVersion"
    compile "com.etaty.rediscala:rediscala_$scalaMainVersion:1.3.1"
    compile "org.apache.tomcat:tomcat-catalina:8.0.9"

    compile "org.xerial.snappy:snappy-java:1.1.1"
    compile "de.ruedigermoeller:fst:1.58"
    compile "com.google.code.gson:gson:2.2.4"

    compile "ch.qos.logback:logback-classic:1.1.2"
    compile "org.slf4j:slf4j-api:$slf4jVersion"
    compile "org.slf4j:slf4j-log4j12:$slf4jVersion"

    // We use Scalatest for testing our library
    testCompile "org.scalatest:scalatest_$scalaMainVersion:2.2.0"
    testRuntime 'org.pegdown:pegdown:1.1.0'
    testCompile "com.github.axel22:scalameter_$scalaMainVersion:0.5-M2"

    // Use Scalatra web framework for Testing
    testCompile "org.scalatra:scalatra_$scalaMainVersion:$scalatraVersion"
    testCompile "org.scalatra:scalatra-scalate_$scalaMainVersion:$scalatraVersion"
    testCompile "org.scalatra:scalatra-scalatest_$scalaMainVersion:$scalatraVersion"
    testCompile "javax.servlet:javax.servlet-api:3.1.0"

    testCompile "org.apache.tomcat.embed:tomcat-embed-core:8.0.9"
    testCompile "org.apache.tomcat.embed:tomcat-embed-logging-log4j:8.0.9"
    //testCompile "org.apache.tomcat.embed:tomcat-embed-jasper:8.0.9"
}

repositories {
    mavenCentral()
    mavenLocal()
    jcenter()

    maven { url "http://repo.typesafe.com/typesafe/releases/" }
    maven { url "http://dl.bintray.com/etaty/maven" }
}

test {
    testLogging {
        // set options for log level LIFECYCLE
        events 'failed'
        exceptionFormat 'short'
        showStandardStreams true

        // set options for log level DEBUG
        debug {
            events "started", "skipped", "failed"
            exceptionFormat "full"
        }
    }
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives javadocJar
}